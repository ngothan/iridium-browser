# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//third_party/closure_compiler/js_unit_tests.gni")
import("//ui/file_manager/base/gn/js_test_type_check.gni")
import("//ui/webui/resources/tools/optimize_webui.gni")
import("//ui/webui/webui_features.gni")

related_apps = [ "//ui/file_manager/file_manager/*" ]

group("closure_compile") {
  testonly = true
  deps = [
    ":closure_compile_jsmodules",
    ":js_test_type_check",
    ":test_support_modules_type_check",
  ]
}

group("js_modules") {
  deps = [
    ":file_manager_base",
    ":file_operation_handler",
    ":file_operation_manager",
    ":file_operation_util",
    ":progress_center",
    ":test_util",
  ]
}

# Default to private.
visibility = [ ":*" ]

js_type_check("closure_compile_jsmodules") {
  deps = [
    ":file_manager_base",
    ":file_operation_handler",
    ":file_operation_manager",
    ":file_operation_util",
    ":mock_progress_center",
    ":progress_center",
    ":runtime_loaded_test_util",
    ":test_util_base",
    ":volume_manager_factory",
    ":volume_manager_impl",
    ":volume_manager_util",
    "//ui/file_manager:js_from_ts",
  ]

  closure_flags = strict_error_checking_closure_args + [
                    "language_in=ECMASCRIPT_2020",
                    "js_module_root=./gen/ui/file_manager/tsc",
                    "js_module_root=./gen/ui/file_manager",
                    "js_module_root=" +
                        rebase_path("//ui/file_manager", root_build_dir),
                    "browser_resolver_prefix_replacements=\"chrome://webui-test/=./\"",
                    "hide_warnings_for=third_party/",
                    "hide_warnings_for=gen/ui/file_manager/tsc/",
                    "browser_resolver_prefix_replacements=\"chrome-extension://pmfjbimdmchhbnneeidfognadeopoehp/=./image_loader/\"",
                    "browser_resolver_prefix_replacements=\"chrome://file-manager/=./file_manager/\"",
                  ]
}

js_type_check("test_support_modules_type_check") {
  testonly = true
  deps = [
    ":mock_crostini",
    ":mock_file_operation_manager",
    ":mock_progress_center",
    ":mock_volume_manager",
    ":runtime_loaded_test_util",
    "//ui/file_manager:js_from_ts",
  ]
  closure_flags =
      strict_error_checking_closure_args + [
        "language_in=ECMASCRIPT_2020",
        "js_module_root=./gen/ui/file_manager/tsc",
        "js_module_root=./gen/ui",
        "js_module_root=" + rebase_path("//ui", root_build_dir),
        "browser_resolver_prefix_replacements=\"chrome://webui-test/=./\"",
        "hide_warnings_for=third_party/",
        "hide_warnings_for=gen/ui/file_manager/tsc/",
      ]
}

js_library("file_manager_base") {
  visibility += [ "//ui/file_manager/file_manager/foreground/js:file_manager" ]
  deps = [
    ":file_operation_handler",
    ":file_operation_manager",
    ":file_operation_util",
    ":progress_center",
    ":volume_manager_factory",
    "//ui/file_manager/file_manager/common/js:files_app_state",
    "//ui/file_manager/file_manager/common/js:url_constants",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
    "//ui/file_manager/file_manager/externs:volume_info",
    "//ui/file_manager/file_manager/externs:volume_manager",
    "//ui/file_manager/file_manager/externs/background:crostini",
    "//ui/file_manager/file_manager/externs/background:drive_sync_handler",
    "//ui/file_manager/file_manager/externs/background:file_manager_base",
    "//ui/file_manager/file_manager/externs/background:file_operation_manager",
    "//ui/file_manager/file_manager/externs/background:progress_center",
  ]
}

js_library("mock_crostini") {
  testonly = true
  deps = [ "//ui/file_manager/file_manager/externs/background:crostini" ]
}

js_library("file_operation_handler") {
  deps = [
    "//ui/file_manager/file_manager/common/js:progress_center_common",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/externs/background:file_operation_manager",
    "//ui/file_manager/file_manager/externs/background:progress_center",
    "//ui/file_manager/file_manager/externs/ts:store",
  ]
}

js_unittest("file_operation_handler_unittest") {
  deps = [
    ":file_operation_handler",
    ":file_operation_util",
    ":mock_file_operation_manager",
    ":mock_progress_center",
    "//chrome/test/data/webui/chromeos:chai_assert",
  ]
}

js_library("mock_file_operation_manager") {
  deps = [
    "//ash/webui/common/resources:assert",
    "//ash/webui/common/resources:event_target",
    "//ui/file_manager/file_manager/externs/background:file_operation_manager",
  ]
  visibility += [
    "//ui/file_manager/file_manager/foreground/js:directory_model_unittest",
  ]
}

js_library("file_operation_manager") {
  deps = [
    ":file_operation_util",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/externs:entry_location",
    "//ui/file_manager/file_manager/externs:files_app_entry_interfaces",
    "//ui/file_manager/file_manager/externs:volume_manager",
    "//ui/file_manager/file_manager/externs/background:file_operation_manager",
  ]
}

js_unittest("file_operation_manager_unittest") {
  deps = [
    ":file_operation_manager",
    ":file_operation_util",
    ":volume_manager_factory",
    "//ash/webui/common/resources:load_time_data.m",
    "//chrome/test/data/webui/chromeos:chai_assert",
    "//ui/file_manager/file_manager/common/js:mock_chrome",
    "//ui/file_manager/file_manager/common/js:mock_entry",
    "//ui/file_manager/file_manager/common/js:test_error_reporting",
    "//ui/file_manager/file_manager/externs:entry_location",
    "//ui/file_manager/file_manager/externs/background:file_operation_manager",
  ]
}

js_library("file_operation_util") {
  deps = [ "//ui/file_manager/file_manager/common/js:util" ]
}

js_library("mock_volume_manager") {
  visibility += related_apps
  deps = [
    ":volume_manager_factory",
    ":volume_manager_impl",
    "//ash/webui/common/resources:assert",
    "//ui/file_manager/file_manager/common/js:mock_entry",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
    "//ui/file_manager/file_manager/externs:entry_location",
    "//ui/file_manager/file_manager/externs:files_app_entry_interfaces",
    "//ui/file_manager/file_manager/externs:volume_info",
    "//ui/file_manager/file_manager/externs:volume_info_list",
    "//ui/file_manager/file_manager/externs:volume_manager",
  ]
}

js_library("mock_progress_center") {
  deps = [
    "//ui/file_manager/file_manager/common/js:progress_center_common",
    "//ui/file_manager/file_manager/externs/background:progress_center",
  ]
}

js_library("progress_center") {
  deps = [
    "//ui/file_manager/file_manager/common/js:async_util",
    "//ui/file_manager/file_manager/common/js:progress_center_common",
    "//ui/file_manager/file_manager/common/js:url_constants",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/externs:progress_center_panel",
    "//ui/file_manager/file_manager/externs/background:progress_center",
  ]
}

js_library("runtime_loaded_test_util") {
  visibility += [ "//ui/file_manager/integration_tests:remote_call" ]

  deps = [
    ":file_operation_util",
    ":volume_manager_factory",
    "//ash/webui/common/resources:assert",
    "//ui/file_manager/file_manager/common/js:error_counter",
    "//ui/file_manager/file_manager/common/js:progress_center_common",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
    "//ui/file_manager/file_manager/externs/background:file_manager_base",
  ]
}

js_library("test_util_base") {
  visibility += related_apps
  deps = [
    ":runtime_loaded_test_util",
    "//ash/webui/common/resources:assert",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
  ]
}

js_library("test_util") {
  deps = [
    ":file_manager_base",
    ":test_util_base",
    "//ui/file_manager/file_manager/common/js:files_app_state",
    "//ui/file_manager/file_manager/common/js:progress_center_common",
    "//ui/file_manager/file_manager/common/js:util",
  ]
}

js_library("volume_manager_impl") {
  deps = [
    ":volume_manager_util",
    "//ash/webui/common/resources:assert",
    "//ash/webui/common/resources:cr_deprecated",
    "//ash/webui/common/resources:event_target",
    "//ui/file_manager/file_manager/common/js:async_util",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
    "//ui/file_manager/file_manager/externs:volume_info",
    "//ui/file_manager/file_manager/externs:volume_manager",
    "//ui/file_manager/file_manager/externs/ts:store",
  ]
}

js_library("volume_manager_factory") {
  deps = [
    ":volume_manager_impl",
    "//ui/file_manager/file_manager/externs:volume_manager",
  ]
}

js_unittest("volume_manager_unittest") {
  deps = [
    ":volume_manager_factory",
    ":volume_manager_impl",
    ":volume_manager_util",
    "//ash/webui/common/resources:load_time_data.m",
    "//chrome/test/data/webui/chromeos:chai_assert",
    "//ui/file_manager/file_manager/common/js:mock_chrome",
    "//ui/file_manager/file_manager/common/js:mock_entry",
    "//ui/file_manager/file_manager/common/js:test_error_reporting",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
  ]
}

js_library("volume_manager_util") {
  deps = [
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
    "//ui/file_manager/file_manager/externs:volume_info",
    "//ui/file_manager/file_manager/externs/ts:store",
  ]
}

js_test_type_check("js_test_type_check") {
  deps = [
    ":file_operation_handler_unittest",
    ":file_operation_manager_unittest",
    ":volume_manager_unittest",
  ]

  closure_flags = strict_error_checking_closure_args + [
                    "language_in=ECMASCRIPT_2020",
                    "js_module_root=gen/ui/file_manager/tsc/",
                    "js_module_root=gen/ui/file_manager/",
                    "js_module_root=" +
                        rebase_path("//ui/file_manager/", root_build_dir),
                    "browser_resolver_prefix_replacements=\"chrome://webui-test/=./\"",
                    "hide_warnings_for=third_party/",
                    "hide_warnings_for=gen/ui/file_manager/tsc/",
                    "browser_resolver_prefix_replacements=\"chrome-extension://pmfjbimdmchhbnneeidfognadeopoehp/=./image_loader/\"",
                    "browser_resolver_prefix_replacements=\"chrome://file-manager/=./file_manager/\"",
                  ]
  extra_deps = [ "//ui/file_manager:js_from_ts" ]
}

tsc_folder =
    rebase_path("$target_gen_dir/../../../tsc/file_manager/background/js",
                root_build_dir)

# Build the test utils that is loaded at runtime by the browser test.
optimize_webui("build") {
  visibility += [ "//ui/file_manager:build_grd" ]
  host = "file-manager"

  input = tsc_folder

  js_module_in_files = [ "runtime_loaded_test_util.js" ]

  deps = [ ":runtime_loaded_test_util" ]
  if (enable_webui_inline_sourcemaps) {
    deps += [ "//ui/file_manager:merge_source_maps" ]
  } else {
    deps += [ "//ui/file_manager:build_ts" ]
  }
}
